colorTest: depth extent: chartExtent colorMapper: colorMapper 
	"Create a palette of colors sorted horizontally by hue and vertically by lightness. Useful for eyeballing the color gamut of the display, or for choosing a color interactively."
	"Note: It is slow to build this palette, so it should be cached for quick access."
	"(Color colorTest: 32 extent: 570@180 colorMapper: [:c | c]) display"
	"(Color colorTest: 32 extent: 570@180 colorMapper:
		[:c | Color
			r: (c red * 7) asInteger / 7
			g: (c green * 7) asInteger / 7
			b: (c blue * 3) asInteger / 3]) display"
	"(Color colorTest: 32 extent: 570@180 colorMapper:
		[:c | Color
			r: (c red * 5) asInteger / 5
			g: (c green * 5) asInteger / 5
			b: (c blue * 5) asInteger / 5]) display"
	"(Color colorTest: 32 extent: 570@180 colorMapper:
		[:c | Color
			r: (c red * 15) asInteger / 15
			g: (c green * 15) asInteger / 15
			b: (c blue * 15) asInteger / 15]) display"
	"(Color colorTest: 32 extent: 570@180 colorMapper:
		[:c | Color
			r: (c red * 31) asInteger / 31
			g: (c green * 31) asInteger / 31
			b: (c blue * 31) asInteger / 31]) display"
	| basicHue x y c startHue palette transHt vSteps transCaption grayWidth hSteps |
	palette := Form 
		extent: chartExtent
		depth: depth.
	transCaption := Form 
		extent: 34 @ 9
		depth: 1
		fromArray: #(
				0
				0
				256
				0
				256
				0
				3808663859
				2147483648
				2491688266
				2147483648
				2491688266
				0
				2491688266
				0
				2466486578
				0
				0
				0
			)
		offset: 0 @ 0.	"(DisplayText text: 'no color' asText textStyle: (TextConstants at: #ComicPlain)) form storeString"
	transHt := transCaption height.
	palette fillWhite: (0 @ 0 extent: palette width @ transHt).
	palette fillBlack: (0 @ transHt extent: palette width @ 1).
	transCaption 
		displayOn: palette
		at: palette boundingBox topCenter - ((transCaption width // 2) @ 0).
	grayWidth := 10.
	startHue := 338.0.
	vSteps := (palette height - transHt) // 2.
	hSteps := palette width - grayWidth.
	x := 0.
	startHue 
		to: startHue + 360.0
		by: 360.0 / hSteps
		do: 
			[ :h | 
			basicHue := Color 
				h: h asFloat
				s: 1.0
				v: 1.0.
			y := transHt + 1.
			0 
				to: vSteps
				do: 
					[ :n | 
					c := basicHue 
						mixed: n asFloat / vSteps asFloat
						with: Color white.
					c := colorMapper value: c.
					palette 
						fill: (x @ y extent: 1 @ 1)
						fillColor: c.
					y := y + 1 ].
			1 
				to: vSteps
				do: 
					[ :n | 
					c := Color black 
						mixed: n asFloat / vSteps asFloat
						with: basicHue.
					c := colorMapper value: c.
					palette 
						fill: (x @ y extent: 1 @ 1)
						fillColor: c.
					y := y + 1 ].
			x := x + 1 ].
	y := transHt + 1.
	1 
		to: vSteps * 2
		do: 
			[ :n | 
			c := Color black 
				mixed: n asFloat / (vSteps * 2) asFloat
				with: Color white.
			c := colorMapper value: c.
			palette 
				fill: (x @ y extent: 10 @ 1)
				fillColor: c.
			y := y + 1 ].
	^ palette